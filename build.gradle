apply plugin: 'java'
apply plugin: 'maven'

group = 'org.scenic-view'
version = '8.0-dp4'

defaultTasks 'install'

repositories {
    mavenLocal()
}

configurations {
    jdk
    binTray
}

dependencies {
    jdk files("${System.properties['java.home']}/../lib/tools.jar")
    try {
        jdk files(svJFXRTJar)
    } catch (MissingPropertyException pne) {
        jdk files("${System.properties['java.home']}/lib/ext/jfxrt.jar")
    }
    compile "org.fxconnector:fxconnector:$version"
    binTray "org.fxconnector:fxconnector:$version"
}

sourceSets {
    main {
        java {
            srcDir 'src'
            include '**/*.java'
            exclude 'deploy/**'
        }
        resources {
            srcDir 'src'
            exclude '**/*.java'
            exclude 'deploy/**'
        }
        compileClasspath += configurations.jdk
    }
}

jar {
    classifier = 'small'
    manifest {
        attributes(
            'Main-Class': 'org.scenicview.utils.ScenicViewBooter',
            'Premain-Class': 'org.scenicview.utils.SVInstrumentationAgent'
        )
    }
}

task jarBinTray(type: Jar, dependsOn: 'jar') {
    from { configurations.binTray.collect { it.isDirectory() ? it : zipTree(it) } }
    from zipTree(jar.archivePath)
    manifest {
        attributes(
                'Main-Class': 'org.scenicview.utils.ScenicViewBooter',
                'Agent-Class': 'org.fxconnector.remote.AgentTest',
                'Premain-Class': 'org.scenicview.utils.SVInstrumentationAgent'
        )
    }
}

artifacts {
    archives(jarBinTray)
    archives(jar) {classifier='small'}
}

uploadArchives() {
    repositories {
        mavenDeployer {
            repository(url: bintray_api_base_url + '/maven/' + bintray_orgname + '/' + bintray_repo + '/' + bintray_package + '/',
                    id: 'bintray'
            ) {
                try {
                    authentication(
                            userName: bintray_username,
                            password: bintray_api_key
                    )
                } catch (MissingPropertyException pne) {
                    // ignore, don't authenticate
                }
            }
        }
    }
}
